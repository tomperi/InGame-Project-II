// room script file

// Interact with the tree
function hHotspot2_Interact()
{
  if (!treeOpen) {
    // Open the tree eye
    if(player.x > 370 && player.x < 700){
      oEye.SetView(4, 0, 0);
      oEye.Visible=true;
      oEye.Animate(0, 2, eOnce, eBlock);
      //dDialog1.Start();
      treeOpen=true;
      Wait(20);
      player.Say("Woho! What the hell is that?");
    }
    else{
      player.Say("What a weird looking tree...");
    }
  } else {
    // Generate tears
    player.Say("I'm going to poke that stupid tree in the yee");
    player.Walk(681, 360, eBlock, eWalkableAreas);
    player.FaceDirection(eDirectionLeft);
    Wait(15);
    oTear.SetView(6, 0, 0);
    oTear.Visible=true;
    oTear.Clickable=true;
    oTear.Animate(0, 4, eOnce, eNoBlock);
    SetTimer(1, 300);
  }
}

function hHotspot2_Talk()
{
  // Talk to the tree
  if(player.x > 370 && player.x < 700){
    //dDialog1.Start();
    player.Say("Hi tree, whats up?");
    Wait(20);
    player.Say("Well I guess he's a mute...");
  }
  else{
    player.Say("What a weird looking tree...");
  }
}

// Interact with jacob
function hHotspot3_AnyClick()
{
  player.FaceCharacter(cJacob);
  if (!talkedToJacob) {
   // if (player.x > 140) 
    player.Animate(1, 0, eOnce, eNoBlock);
    oLeg.Move(355, 345, 1, eBlock, eAnywhere);
    oLeg.Visible=false;
    cJacob.Animate(1, 2, eOnce, eBlock);
    cJacob.Animate(2, 0, eOnce, eBlock);
    cJacob.Say("Hey kid, never seen you around. What are you up to?");
    dDialog2.Start();
    talkedToJacob=true;
    dDialog1.Start();
  } else{  
    dDialog1.Start();
  }
}

// Interact with the hole in the tree
function hHotspot1_Interact()
{
  return;
 }
function hHotspot4_WalkOn()
{
 oPlantArea.Clickable = 1;
}

function oPlantArea_AnyClick()
{
//  Check if player has items for flower, if not Display
// ("This seemslike a great place for a flower!")
  return;
}

function room_Load()
{
  aTrack_2.Play(eAudioPriorityNormal, eRepeat);
  gIconbar.Visible=true;
  gIconbar2.Visible=true;
}

// Planet the seed and water it 
function hHotspot4_UseInv()
{
  if (cChar1.ActiveInventory == iSeed) {
    player.Say("I'm going to plant the seed in that hole");
    player.Walk(820, 350, eBlock, eWalkableAreas); 
    player.FaceDirection(eDirectionRight);
    oPlantedSeed.Visible = true;
    cChar1.LoseInventory(iSeed);
    mouse.ChangeModeView(eModeWalkto, 0);
    player.Say("The seed is strong with this one!");
  } 
  
  if (player.ActiveInventory == iHatWithWater) {
    if (oPlantedSeed.Visible) {
      player.LoseInventory(iHatWithWater);
      oFlower.SetView(7, 0, 0);
      oPlantedSeed.Visible = false;
      oFlower.Visible = true;
      oFlower.Animate(0, 2, eOnce, eBlock);
    } else {
      player.Say("I'm not going to waste my water on an empty hole");
    }
  }
}

function room_RepExec()
{
  if (IsTimerExpired(1))
    oTear.Visible=false;
}

function oTear_Interact()
{
  player.Say("I can't catch that with my bare hands");
}

// Catch the tear with the hat!


function oTear_UseInv()
{
  if (cChar1.ActiveInventory == iHat) {
    oTear.Visible=false;
    player.Say("Woho, I have some water in the hat");
    cChar1.LoseInventory(iHat);
    player.AddInventory(iHatWithWater);
  }
}
function oFlower_Interact()
{
  if (player.x < 790) 
    player.Walk(800, 370, eBlock, eWalkableAreas);
  if (player.x > 990)
    player.Walk(970, 370, eBlock, eWalkableAreas);
  Wait(10);
  finishedDarkForest = true;
  aTrack_2.Stop();
  cJacob.ChangeRoom(1, 680, 365, eDirectionRight);
  player.ChangeRoom(1, 900, 335, eDirectionLeft);
}

function oFlower_Look()
{
    player.Say("A FUCKING FLOWER!!! WE CAN GO BACK HOME!!");
}

function oLeg_Look()
{
 player.Say("Is that a... shoe...?");
}

function oLeg_Interact()
{
  hHotspot3_AnyClick();
}

function oObject2_Interact()
{
  hHotspot3_AnyClick();
}

function hHotspot1_Look()
{
  if(room3empty){
    player.Say("There's nothing interesting in there...");
  }
  else{
    if (player.x < 190) {
      player.Say("Oh... what's in here?");
      Wait(2);
      player.ChangeRoomAutoPosition(3);
    } else {
      player.Say("I can't see from so far away");
    }
  }
}

function oPlantedSeed_Interact()
{
  player.Say("You want me to pick it up after I just put it in the ground?");
  player.Say("Not going to happen.");
}

function oPlantedSeed_Look()
{
  player.Say("Well, now the seed is in the ground. What's next?");
}

function oPlantedSeed_Talk()
{
   player.Say("Hey seed, please grow!");
   player.Say("I guess you need more then just words to do that...");
}
